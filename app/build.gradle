plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "androidx.navigation.safeargs"
    id "com.google.firebase.crashlytics"
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "pl.kapucyni.wolczyn.app"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 23
        versionName "2022.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def weatherApiKey = properties.getProperty("weather_api_key", "")
        def kapucyniApiKey = properties.getProperty("kapucyni_api_key", "")
        def googleSignInKey = properties.getProperty("google_sign_in_key", "")
        it.buildConfigField("String", "WEATHER_API_KEY", weatherApiKey)
        it.resValue("string", "weather_api_key", weatherApiKey)
        it.buildConfigField("String", "KAPUCYNI_API_KEY", kapucyniApiKey)
        it.resValue("string", "kapucyni_api_key", kapucyniApiKey)
        it.buildConfigField("String", "GOOGLE_SIGN_IN_KEY", googleSignInKey)
        it.resValue("string", "google_sign_in_key", googleSignInKey)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'pl.kapucyni.wolczyn.app'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "com.google.android.material:material:1.7.0-alpha02"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // DrawerLayout
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"

    // Preferences
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.5.0"
    kapt "com.android.databinding:compiler:3.2.0-alpha10"

    // Firebase
    implementation "com.google.firebase:firebase-core:21.0.0"
    implementation "com.google.firebase:firebase-common-ktx:20.1.1"
    implementation "com.google.firebase:firebase-firestore:24.2.0"
    implementation "com.google.firebase:firebase-firestore-ktx:24.2.0"
    implementation "com.google.firebase:firebase-crashlytics:18.2.11"
    implementation "com.google.firebase:firebase-analytics:21.0.0"
    implementation "com.google.firebase:firebase-analytics-ktx:21.0.0"

    // Google auth
    implementation "com.google.android.gms:play-services-auth:20.2.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    // JSoup
    implementation "org.jsoup:jsoup:1.14.3"

    // Chrome Custom Tabs
    implementation "androidx.browser:browser:1.4.0"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"

    // Retrofit2
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // OkHttp3
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.1"

    // Chart library
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    // Tests
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}
apply plugin: 'com.google.gms.google-services'
